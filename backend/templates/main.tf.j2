terraform {
  required_version = ">= 1.6"
  required_providers {
    aws   = { source = "hashicorp/aws", version = ">= 5.0" }
    tls   = { source = "hashicorp/tls", version = ">= 4.0" }
    local = { source = "hashicorp/local", version = ">= 2.0" }
  }
}

provider "aws" {
  region = "{{ region }}"
}

# --- Networking ---
resource "aws_vpc" "bpp_vpc" {
  cidr_block           = "{{ vpc_cidr }}"
  enable_dns_hostnames = true
  enable_dns_support   = true
  tags = { Name = "{{ name_prefix }}-vpc" }
}

resource "aws_subnet" "bpp_subnet" {
  vpc_id                  = aws_vpc.bpp_vpc.id
  cidr_block              = "{{ subnet_cidr }}"
  availability_zone       = "{{ az }}"
  map_public_ip_on_launch = true
  tags = { Name = "{{ name_prefix }}-subnet" }
}

resource "aws_internet_gateway" "bpp_igw" {
  vpc_id = aws_vpc.bpp_vpc.id
  tags   = { Name = "{{ name_prefix }}-igw" }
}

resource "aws_route_table" "bpp_route_table" {
  vpc_id = aws_vpc.bpp_vpc.id
  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.bpp_igw.id
  }
  tags = { Name = "{{ name_prefix }}-rt" }
}

resource "aws_route_table_association" "bpp_subnet_association" {
  subnet_id      = aws_subnet.bpp_subnet.id
  route_table_id = aws_route_table.bpp_route_table.id
}

# --- Security group ---
resource "aws_security_group" "bpp_security_group" {
  name        = "{{ name_prefix }}-sg"
  description = "allow ssh + web (tcp/22,80)"
  vpc_id      = aws_vpc.bpp_vpc.id

  ingress {
    description = "SSH"
    from_port   = 22
    to_port     = 22
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  ingress {
    description = "Web"
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = { Name = "{{ name_prefix }}-sg" }
}

# --- Key pair (tạo mới) ---
resource "tls_private_key" "bpp_keypair" {
  algorithm = "RSA"
  rsa_bits  = 2048
}

resource "aws_key_pair" "bpp_keypair" {
  key_name   = "{{ key_name }}"
  public_key = tls_private_key.bpp_keypair.public_key_openssh
}

# Ghi private key ra file cạnh main.tf (cẩn trọng state!)
resource "local_file" "private_key_pem" {
  content         = tls_private_key.bpp_keypair.private_key_pem
  filename        = "${path.module}/{{ key_name }}.pem"
  file_permission = "0600"
}

# --- EC2 instances ---
resource "aws_instance" "bpp_instance" {
  count                       = {{ instance_count }}
  ami                         = "{{ ami }}"
  instance_type               = "{{ instance_type }}"
  key_name                    = aws_key_pair.bpp_keypair.key_name
  subnet_id                   = aws_subnet.bpp_subnet.id
  vpc_security_group_ids      = [aws_security_group.bpp_security_group.id]
  associate_public_ip_address = true
  user_data                   = file("{{ user_data_path }}")

  tags = {
    Name = "{{ name_prefix }}-${count.index + 1}"
  }
}

# --- NLB + target group/listener ---
resource "aws_lb" "bpp_lb" {
  name                       = "{{ name_prefix }}-nlb"
  load_balancer_type         = "network"
  internal                   = false
  enable_deletion_protection = false

  subnet_mapping {
    subnet_id = aws_subnet.bpp_subnet.id
  }

  tags = { Name = "{{ name_prefix }}-nlb" }
}

resource "aws_lb_target_group" "bpp_target_group" {
  name     = "{{ name_prefix }}-tg"
  port     = 80
  protocol = "TCP"
  vpc_id   = aws_vpc.bpp_vpc.id

  health_check {
    protocol = "TCP"
    port     = "80"
  }

  tags = { Name = "{{ name_prefix }}-tg" }
}

resource "aws_lb_target_group_attachment" "bpp_attachment" {
  count            = length(aws_instance.bpp_instance)
  target_group_arn = aws_lb_target_group.bpp_target_group.arn
  target_id        = aws_instance.bpp_instance[count.index].id
  port             = 80
}

resource "aws_lb_listener" "bpp_listener" {
  load_balancer_arn = aws_lb.bpp_lb.arn
  port              = 80
  protocol          = "TCP"

  default_action {
    type             = "forward"
    target_group_arn = aws_lb_target_group.bpp_target_group.arn
  }
}

# --- Outputs ---
output "instance_dns" {
  value = aws_instance.bpp_instance[*].public_dns
}

output "instance_public_ip" {
  value = aws_instance.bpp_instance[*].public_ip
}

output "nlb_dns_name" {
  value = aws_lb.bpp_lb.dns_name
}
